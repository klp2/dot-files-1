#!/usr/bin/env perl

# On OS X, open up the Github URL for the repository which you are currently
# in.

use strict;
use warnings;
use feature qw( say );

use Browser::Open qw( open_browser );
use URI;
use URI::git;    # ensure it's installed
use URI::Heuristic qw(uf_uristr);

my $remote = shift @ARGV || 'origin';
my $output = `git remote get-url --all $remote`;

my @lines = split m{\n}, $output;

foreach my $line (@lines) {
    my $resource = URI->new($line);

    # remove 'git@' from git@github.com:username/repo.git
    $resource =~ s{\w*\@}{};

    # remove : from git@github.com:username/repo.git
    $resource =~ s{(\w):(\w)}{$1/$2};

    my $uri = URI->new( uf_uristr($resource) );
    $uri->scheme('https');

    open_browser($uri);
    exit;
}
