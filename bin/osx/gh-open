#!/usr/bin/env perl

# On OS X, open up the Github URL for the repository which you are currently
# in.

use strict;
use warnings;
use feature qw( say );

use Browser::Open qw( open_browser );
use URI;
use URI::Heuristic qw(uf_uristr);

my $output = `git remote -v`;

my @lines = split m{\n}, $output;

foreach my $line ( @lines ) {
    my @parts = split m{\s+}, $line;
    next unless $parts[0] eq 'origin' && $parts[2] eq '(fetch)';

    my $resource = $parts[1];

    # remove 'git@' from git@github.com:username/repo.git
    $resource =~ s{\w*\@}{};

    # remove : from git@github.com:username/repo.git
    $resource =~ s{(\w):(\w)}{$1/$2};

    my $uri = URI->new( uf_uristr( $resource ) );
    $uri->scheme( 'https' );

    open_browser( $uri );
    exit;
}
